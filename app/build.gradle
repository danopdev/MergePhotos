plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    signingConfigs {
        'default' {
            File keystoreConfigFile = file('keystore.config')
            if (keystoreConfigFile.exists()) {
                Properties keystoreProps = new Properties()
                keystoreProps.load(new FileInputStream(file('keystore.config')))

                keyAlias keystoreProps['keyAlias']
                keyPassword keystoreProps['keyPassword']
                storePassword keystoreProps['storePassword']
                storeFile file(keystoreProps['storePath'])
            }
        }
    }

    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.dan.mergephotos"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 5
        versionName '1.15'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-frtti -fexceptions"
                abiFilters 'arm64-v8a'
                arguments "-DANDROID_STL=c++_shared"
            }
        }
    }

    buildTypes {
        debug {
            if (file('keystore.config').exists()) {
                signingConfig signingConfigs.'default'
            }
        }

        release {
            //minifyEnabled true
            //shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.10.2"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        dataBinding true
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    sourceSets {
        main {
            jni {
                srcDirs 'src/main/cpp'
            }
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation project(path: ':opencv')
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation 'com.github.MikeOrtiz:TouchImageView:3.0.6'
    implementation "androidx.documentfile:documentfile:1.0.1"
    implementation("androidx.exifinterface:exifinterface:1.3.3")
}
